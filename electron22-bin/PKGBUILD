# Maintainer: Nicola Squartini <tensor5@gmail.com>

_pkgname=electron22
pkgname=electron22-bin
pkgver=22.3.27
pkgrel=1
pkgdesc='Build cross platform desktop apps with web technologies'
arch=('x86_64' 'loong64')
url='https://electronjs.org/'
license=('MIT' 'custom')
depends=('c-ares' 'ffmpeg' 'gtk3' 'libevent' 'libxslt' 'minizip' 'nss' 're2'
         'snappy')
optdepends=('kde-cli-tools: file deletion support (kioclient5)'
            'libappindicator-gtk3: StatusNotifierItem support'
            'pipewire: WebRTC desktop sharing under Wayland'
            'trash-cli: file deletion support (trash-put)'
            "xdg-utils: open URLs with desktop's default (xdg-email, xdg-open)")
conflicts=($pkgname)
provides=($pkgname)
source=("http://public.loongarch.dev/sources/electron/electron-v${pkgver}-linux-loong64.zip/61cb1c08b4319470a74c050dcfb5e600/electron-v${pkgver}-linux-loong64.zip"
        "icon.png::http://public.loongarch.dev/sources/electron/electron17.png/5fbf91dbab04c5f2b19e1aca8f87a307/electron17.png"
        "electron-launcher.sh"
        "electron.desktop"
       )
noextract=("electron-v${pkgver}-linux-loong64.zip")
sha256sums=('b96c9d980df8a23e152a7621ee7127de365d5f3f9b55527d2b7c9a859792d475'
            'b0f6f06d7e94053dabcc7dd8adc83d925089dbca55df0ca573b267b710e1f79d'
            'e612317aee7e248ad930251d9d52866eb4bf8118ee0fb0cd9247008e969f543a'
            '3e6725aef3a92a9e0e9e7ecbc02a824faad3b21dada64033c7b5d15b6266aad7')

package() {
  install -dm755 "${pkgdir:?}/usr/lib/${_pkgname}"
  bsdtar -xf electron-v${pkgver}-linux-loong64.zip -C "${pkgdir}/usr/lib/${_pkgname}"

  chmod u+s "${pkgdir}/usr/lib/${_pkgname}/chrome-sandbox"

  install -dm755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  for l in "${pkgdir}/usr/lib/${_pkgname}"/{LICENSE,LICENSES.chromium.html}; do
    ln -s  \
      "$(realpath --relative-to="${pkgdir}/usr/share/licenses/${_pkgname}" "${l}")" \
      "${pkgdir}/usr/share/licenses/${_pkgname}"
  done

  install -Dm755 "${srcdir}/electron-launcher.sh" \
    "${pkgdir}/usr/bin/${_pkgname}"
  if [[ "${_use_suffix}" == 0 ]]; then
    ln "${pkgdir}/usr/bin/${_pkgname}" \
      "${pkgdir}/usr/bin/${_pkgname}${_major_ver}"
  fi

  # Install .desktop and icon file (see default_app-icon.patch)
  install -Dm644 electron.desktop \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 icon.png \
          "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"  # hicolor has no 1024x1024
}
